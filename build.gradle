/**
 * Gradle build file for the Whiteflag Java Library
 */

/* Plugins */
plugins {
    id 'java'
    id 'java-library'
    id 'java-library-distribution'
    id 'distribution'
    id 'maven-publish'
}
/* Project information */
version = '1.2.0-dev'
group = 'org.whiteflagprotocol.java'

/* Display basic information */
println  "Configuration for ${project.rootProject}";
println  "  project:    ${project.name}"
println  "  group:      ${project.group}"
println  "  version:    ${project.version}";
println  "  directory:  ${project.projectDir}";

/* Repositories and Dependencies */
repositories {
    mavenCentral()
}
dependencies {
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.networknt:json-schema-validator:1.0.87'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.76'
}
/* Metadata */
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}
/* Testing */
test {
    testLogging {
        events "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
    }
}
/* Distribution */
distributions {
    main {
        distributionBaseName = project.name
    }
}
/* Include documentation and source */
java {
    withJavadocJar()
    withSourcesJar()
}
/* Documentation */
javadoc {
    options.tags = [ 'wfver:a:Whiteflag version:',
                     'wfref:a:Whiteflag standard x-reference:' ]
    options.stylesheetFile = project.file("src/main/javadoc/style.css")
    options.overview = "src/main/javadoc/overview.html"
}
task docs(type: Copy) {
    dependsOn javadoc
    from "$buildDir/docs/javadoc"
    into "docs/javadoc"
}
/* Publishing library as package */
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/whiteflagprotocol/whiteflag-java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from(components.java)
            pom {
                description = 'Implementation of the Whiteflag Protocol for Java development'
                url = 'https://java.whiteflagprotocol.org'
            }
        }
    }
}
